plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.google.protobuf" version "0.8.11"
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.gradle.org/gradle/libs-releases'
    }
}

sourceSets {
    libsDirName = 'lib'
    main {
        proto {
            srcDir '../proto'
        }
    }
    generated {
        java.srcDirs = [
            "${buildDir}/generated/sources/annotationProcessor/java/main", // dagger
            "${buildDir}/generated/source/proto/main/java"                 // protobuf
        ]
    }
}


protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.3'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    compile 'com.google.protobuf:protobuf-javalite:3.11.3'
    compile 'org.gradle:gradle-tooling-api:6.1.1'
    compile "org.java-websocket:Java-WebSocket:1.4.0"
    compile "com.google.dagger:dagger:2.26"
    annotationProcessor "com.google.dagger:dagger-compiler:2.26"
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.30'
    testImplementation 'junit:junit:4.13'
    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
}

application {
    mainClassName = 'com.github.badsyntax.gradletasks.Application'
}

shadowJar {
    minimize()
}

task createStartScripts(type: CreateStartScripts) {
    dependsOn shadowJar
    outputDir = file('build/customScripts')
    mainClassName = application.mainClassName
    applicationName = project.name
    classpath = files(shadowJar.outputs.files)
}

task generateLib(type: Copy) {
    dependsOn build, createStartScripts

    from file("$buildDir/lib/${project.name}-all.jar")
    into file("$buildDir/../../lib/${project.name}-all.jar")

    from "$buildDir/customScripts/${project.name}", "$buildDir/customScripts/${project.name}.bat"
    into "$buildDir/../../lib"
}
