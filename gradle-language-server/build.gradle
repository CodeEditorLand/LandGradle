plugins {
  id "java"
  id "application"
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

repositories  {
  mavenCentral()
  maven {
    url "https://groovy.jfrog.io/artifactory/plugins-release"
  }
}

application {
  applicationName = "gradle-language-server"
  mainClass = "com.microsoft.gradle.GradleLanguageServer"
}

dependencies {
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:0.19.0"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.19.0"
  implementation "org.codehaus.groovy:groovy-eclipse-batch:4.0.16-03"
  implementation "com.google.code.gson:gson:2.9.1"
  implementation "org.apache.bcel:bcel:6.6.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

ext.mainClass = "com.microsoft.gradle.GradleLanguageServer"

task customFatJar(type: Jar) {
    manifest {
      attributes(
        "Main-Class": "com.microsoft.gradle.GradleLanguageServer",
      )
    }
    archiveBaseName = 'ls-fat-jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    destinationDirectory = file('../gradle-server/build/libs')
    with jar
}

def mainJavaDir = 'src/main/java';
def mainResourcesDir = 'src/main/resources';

sourceSets {
  main {
    java {
      srcDirs mainJavaDir
    }
    resources {
      srcDirs mainResourcesDir
    }
  }
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    importOrder()
    eclipse()
    removeUnusedImports()
    trimTrailingWhitespace()
    targetExclude "build/**"
  }
}

compileJava.dependsOn 'spotlessCheck'

project.tasks.named("processResources") {
  duplicatesStrategy = 'include'
}
