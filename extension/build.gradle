description = 'vscode-gradle :: extension'

sourceSets {
    main {
        proto {
            srcDir file('../proto')
        }
    }
}

dependencies {
    compile project(':tasks-server')
}

protobuf {
    // generatedFilesBaseDir = "$projectDir/out"
    plugins {
        grpc {
            path = file("./node_modules/.bin/grpc_tools_node_protoc_plugin")
        }
        ts {
            path = file("./node_modules/.bin/protoc-gen-ts")
        }
    }
    // TODO: generate typescript
    generateProtoTasks {
        generateTestProto.enabled = false;
        extractTestProto.enabled = false;
        extractIncludeProto.enabled = false;
        extractIncludeTestProto.enabled = false;
        all().each { task ->
            task.plugins {
                grpc {
                    outputSubDir = 'js'
                }
                ts {
                    // outputSubDir = '../../src/main/proto'
                    option 'service=grpc-node'
                }
            }
            task.builtins {
                remove java
                js {
                    // outputSubDir = 'proto'
                    option 'import_style=commonjs'
                }
            }
            task.dependsOn npmInstall
            copyProtoJs.dependsOn task
            copyProtoTs.dependsOn task
        }
    }
}

compileJava.enabled = false
compileTestJava.enabled = false

clean {
    delete file("node_modules")
}

task copyProtoJs(type: Copy) {
    dependsOn build
    from "$protobuf.generatedFilesBaseDir/main/js"
    into "out/proto/"
    filter { line ->
        line.replaceAll("\'grpc\'", "\'@grpc/grpc-js\'")
    }
}

task copyProtoTs(type: Copy) {
    dependsOn build
    from "$protobuf.generatedFilesBaseDir/main/ts"
    into "src/proto/"
    filter { line ->
        line.replaceAll('from \"grpc\"', 'from \"@grpc/grpc-js\"')
    }
}

def npmBinDir = "./node_modules/.bin"

task npmInstall(type: Exec) {
  inputs.file("package-lock.json")
  outputs.dir("node_modules")
  commandLine "npm", "install"
}

task lint(type: Exec) {
  dependsOn build
  commandLine "npm", "run", "lint"
}

task typescript(type: Exec) {
  dependsOn build, lint, copyProtoJs, copyProtoTs
  buildDir = 'out'
  outputs.dir("$buildDir")
  commandLine "npm", "run", "compile:ts"
}

build.finalizedBy typescript
build.dependsOn npmInstall
