description = 'vscode-gradle :: extension'

def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
def docsFiles = [
  "README.md",
  "LICENSE.md",
  "ARCHITECTURE.md",
  "CONTRIBUTING.md",
  "FEATURES.md",
  "CHANGELOG.md"
]

sourceSets {
  main {
    proto {
      srcDir file('../proto')
    }
  }
}

protobuf {
  plugins {
    grpc {
      path = file(
        './node_modules/.bin/grpc_tools_node_protoc_plugin' + (isWindows ? '.cmd' : '')
      )
    }
    ts {
      path = file(
        './node_modules/.bin/protoc-gen-ts' + (isWindows ? '.cmd' : '')
      )
    }
  }
  generateProtoTasks {
    generateTestProto.enabled = false
    extractProto.enabled = false;
    extractIncludeProto.enabled = false;
    extractIncludeTestProto.enabled = false;
    generateProto.finalizedBy copyProtoJs, copyProtoTs
    all().each { task ->
      task.plugins {
        grpc {
          outputSubDir = 'js'
        }
        ts {
          option 'service=grpc-node'
        }
      }
      task.builtins {
        remove java
        js {
          option 'import_style=commonjs'
        }
      }
      task.dependsOn npmInstall
    }
  }
}

test.enabled = false;
jar.enabled = false;
compileJava.enabled = false
compileTestJava.enabled = false
processResources.enabled = false;

clean {
  dependsOn ':tasks-server:clean'
  delete protobuf.generatedFilesBaseDir
  delete file('node_modules')
  delete file('src/proto')
  delete file('out')
  delete file('.vscode-test')
  delete file('build')
  delete docsFiles
}

task copyProtoJs(type: Copy) {
  dependsOn ':extension:generateProto'
  group 'copy'
  description 'Copies proto JavaScript definitions into src dir'
  from "$protobuf.generatedFilesBaseDir/main/js"
  into 'out/proto/'
  filter { line ->
    line.replaceAll("\'grpc\'", "\'@grpc/grpc-js\'")
  }
}

task copyProtoTs(type: Copy) {
  dependsOn ':extension:generateProto'
  group 'copy'
  description 'Copies proto TypeScript definitions into src dir'
  from "$protobuf.generatedFilesBaseDir/main/ts"
  into 'src/proto/'
  filter { line ->
    line.replaceAll('from \"grpc\"', 'from \"@grpc/grpc-js\"')
  }
}

task npmInstall(type: CrossPlatformExec) {
  description 'Installs node dependencies'
  inputs.file('package-lock.json')
  outputs.dir('node_modules')
  commandLine 'npm', 'install'
}

task lint(type: CrossPlatformExec) {
  dependsOn npmInstall
  description 'Lints source files'
  commandLine 'npm', 'run', 'lint'
}

task format(type: CrossPlatformExec) {
  description 'Formats source files'
  commandLine 'npm', 'run', 'lint:fix'
}

task buildTypeScript(type: CrossPlatformExec) {
  dependsOn assemble, ':tasks-server:generateProto', copyProtoJs, copyProtoTs
  group 'build'
  description 'Builds TypeScript source files'
  buildDir = 'out'
  outputs.dir("$buildDir")
  inputs.dir('src')
  commandLine 'npm', 'run', 'compile:ts'
}

task watchTypeScript(type: CrossPlatformExec) {
  dependsOn assemble, ':tasks-server:generateProto', copyProtoJs, copyProtoTs
  group 'build'
  description 'Watches and builds TypeScript source files'
  commandLine 'npm', 'run', 'watch:ts'
}

task testVsCode(type: CrossPlatformExec) {
  dependsOn assemble, copyProtoJs, copyProtoTs
  group 'verification'
  description 'Tests the extension'
  commandLine 'npm', 'run', 'test'
}

task copyDocs(type: Copy) {
  group 'copy'
  description 'Copies docs from the root project into the extension'
  from docsFiles.collect { "$rootProject.projectDir/" + it }
  into "$projectDir"
}

task prepareForPublish() {
  dependsOn build, copyDocs
}

build.dependsOn ':tasks-server:build'
assemble.finalizedBy buildTypeScript
assemble.dependsOn ':tasks-server:build'
